entrypoints Rels, Rel ;

RRels. Rels ::= [Rel] ;

terminator Rel "" ;

RTable.           Rel3 ::= Ident ;
RSelect.          Rel2 ::= "\\sigma_{" Cond "}" Rel2 ;
RProject.         Rel2 ::= "\\pi_{" [Projection] "}" Rel2 ;
RRename.          Rel2 ::= "\\rho_{" Renaming "}" Rel2 ;
RGroup.           Rel2 ::= "\\gamma_{" [Ident] "," [Aggregation] "}" Rel2 ;
RSort.            Rel2 ::= "\\tau_{" [SortExp] "}" Rel2 ;
RDistinct.        Rel2 ::= "\\delta" Rel2 ;
RUnion.           Rel1 ::= Rel1 "\\cup" Rel2 ;
RIntersect.       Rel1 ::= Rel1 "\\cap" Rel2 ;
RCartesian.       Rel1 ::= Rel1 "\\times" Rel2 ;
RExcept.          Rel1 ::= Rel1 "\\backslash" Rel2 ;
RNaturalJoin.     Rel1 ::= Rel1 "\\bowtie" Rel2 ;
RThetaJoin.       Rel1 ::= Rel1 "\\bowtie_{" Cond "}" Rel2 ;
RInnerJoin.       Rel1 ::= Rel1 "\\bowtie_{" [Ident] "}" Rel2 ;
RFullOuterJoin.   Rel1 ::= Rel1 "\\bowtie^{o}_{" [Ident] "}" Rel2 ;
RLeftOuterJoin.   Rel1 ::= Rel1 "\\bowtie^{oL}_{" [Ident] "}" Rel2 ;
RRightOuterJoin.  Rel1 ::= Rel1 "\\bowtie^{oR}_{" [Ident] "}" Rel2 ;
RLet.             Rel  ::= "\\mbox{\\textbf{let}}" Ident "=" Rel1 "\\mbox{\\textbf{\\;in\\;}}" Rel ;

coercions Rel 3 ;

CEq.        Cond2 ::= Exp "=" Exp ;
CNEq.       Cond2 ::= Exp "<>" Exp ;
CLt.        Cond2 ::= Exp "<" Exp ;
CGt.        Cond2 ::= Exp ">" Exp ;
CLeq.       Cond2 ::= Exp "<=" Exp ;
CGeq.       Cond2 ::= Exp ">=" Exp ;
CLike.      Cond2 ::= Exp "LIKE" Exp ;
CNot.       Cond2 ::= "NOT" Cond3 ;
CAnd.       Cond1 ::= Cond1 "AND" Cond2 ;
COr.        Cond1 ::= Cond1 "OR" Cond2 ;

coercions Cond 3 ;

EIdent.     Exp3  ::= Ident ;
EQIdent.    Exp3  ::= Ident "." Ident ;
EString.    Exp3  ::= String ;
EInt.       Exp3  ::= Integer ;
EFloat.     Exp3  ::= Double ;
EAggr.      Exp3  ::= Function "(" Distinct Ident ")" ;
EMul.       Exp2  ::= Exp2 "*" Exp3 ;
EDiv.       Exp2  ::= Exp2 "/" Exp3 ;
ERem.       Exp2  ::= Exp2 "%" Exp3 ;
EAdd.       Exp1  ::= Exp1 "+" Exp2 ;
ESub.       Exp1  ::= Exp1 "-" Exp2 ;

coercions Exp 3 ;

separator nonempty Exp "," ;

PExp.        Projection  ::= Exp ;
PRename.     Projection  ::= Exp "\\rightarrow" Ident ;

separator nonempty Projection "," ;

RRelation.   Renaming    ::= Ident ;
RAttributes. Renaming    ::= Ident "(" [Ident] ")" ;

AApp.        Aggregation ::= Function "(" Distinct Ident ")" ;
ARename.     Aggregation ::= Function "(" Distinct Ident ")" "\\rightarrow" Exp ;

separator Aggregation "," ;

FAvg.   Function ::= "AVG" ;
FSum.   Function ::= "SUM" ;
FMax.   Function ::= "MAX" ;
FMin.   Function ::= "MIN" ;
FCount. Function ::= "COUNT" ;

DNone.     Distinct ::= ;
DDistinct. Distinct ::= "\\delta" ; -- used in aggregation

SEAsc.  SortExp  ::= Exp ;
SEAsc.  SortExp  ::= Exp "\\uparrow" ;
SEDesc. SortExp  ::= Exp "\\downarrow" ;

separator nonempty SortExp "," ;

separator Ident "," ;

comment "\\d" ;
comment "\\b" ;
comment "\\[" ;
comment "\\]" ;
comment "\\e" ;
comment "\\H" ;
comment "\\n" ;
comment "%" ;
